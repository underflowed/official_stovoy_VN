label hatstream:
    "Early the next day..."
    scene bg room hat with fade
    show h happy at truecenter
    h "gee i sure am gay"
    h "you know i cant stop thinking about that one npc that salutes you back in vanilla wow."
    h "fuck it i just feel like playing vanilla wow now."
    "Hatkii begins the day by downloading the client of a vanilla World of Warcraft private server onto his PC."
    h "woah... i should stream this..."

    scene bg wow hat
    play music "wowmusic.ogg" #ashenvale or some shit
    with fade
    show h happy at right
    h "wow i sure am a good wow streamer"
    "Stovoy enters Hatkii's Twitch chat."
    show s normal at left
    s "Jesus Christ what the hell is this.{w} Why is the stream in 2 FPS, this isn't Ocarina of Time on N64."
    h "oh hi stovoy this is my wow stream! check out vanilla wow! isn't it so cool???"
    s "Why are you even playing on a private server, Blizzard is releasing WoW Classic soon."
    h "dude idk i just wanted to play vanilla wow now{p}anyways wanna play with me on this server?"
    s angry "Why would I ever want to do anything with you."
    show h happy:
        zoom 2.0 # zoom 1.5 maybe better for sprite we pick? not sure
    h "... i mean your stream's dead anyways"
    s worried "fuck you hatkii{nw}"
    show h happy:
        zoom 1.0
    s normal "Alright Hatkii, I'll play this WoW server with you, but you must entertain me with the act of being unable to download anything."
    h normal "what? what does that mean?"
    s "Don't worry about it. ( ͡° ͜ʖ ͡°)"
    h "ok..."
    scene black
    stop music fadeout 5
    with fade
    "But little did poor Hatkii know, that he was in for a world of pain.{p}i guess that means he should worry about it{nw}"

label hatqbt:
    scene bg hat qbt #he doesn't have his streams auto-vod so i can't grab any screenshots of him trying to use qbittorrent for the first time, what do we do for this lmao
    show h normal at right
    show s think at left
    play music "<to 135>carnivalnightact1.ogg"
    with fade
    s "Alright Hatkii, you gotta torrent the client for this private server."
    h "ughh, this sounds like a lot of effort"
    s "This server actually has players on it, unlike the one you were playing on originally, that had 0 people on it."
    h "alright, fine. {w}what's a torrent?"
    s worried "Are you fucking kidding me right now?  You've never torrented anything before?"
    h "no i guess not"
    s scheming "Alright, go download μTorrent."
    "Maavrik finally wakes up after having fallen asleep earlier, due to watching a really boring game being played on Hatkii's stream."
    show m kukuru flip as m at Position(xpos=0.00, xanchor=0.0, ypos=0.5, yanchor=0.5) behind s
    m "wtf? don't tell him to download a virus smh go get qbittorrent"
    s "Yeah, Maavrik's right go get qBitTorrent. hehehe"
    m "why are you messing with hatkii smh"
    s "Why aren't you?"
    m "good point"
    "And so, Stovoy and Maavrik pranked Hatkii for a good hour, which included hopping onto voice chat with him, and making fun of him while watching him struggle to operate qBitTorrent."
    m "we should've told him to use deluge :^)"
    s "And you call me cruel?"
    m kukukinu flip "HATKII WHY THE FUCK ARE YOU INSTALLING THE GAME TO C:\\Users\\Public\\Documents"
    h sad "WHAT? WHAT AM I DOING WRONG?"
    "Hatkii cancels the extraction."
    s "Actually, that wasn't exactly a bad location to install to or anything, it's just weird."
    m kukuru flip "they probably did that to avoid permission errors lol"
    h angry "well fuck you maavrik i'm going to start installing again now"
    h "wait it has to re-extract?"
    m "{image=omegalul.png} pranked"
    h "fucking maavrikammmmmmmmmmmmmmmmmmmmmmmmmmmmmmmmmmmmmmmmmmmmmmmmmmmmmmmmmmmmmmmmmmmm"
    scene black
    stop music fadeout 5
    with fade

label wowstream:
    "Finally, after Hatkii spent close to 2 hours installing, while Stovoy spent closer to 15 minutes, Stovoy began streaming WoW."
    scene bg wow with fade
    play music "wowmusic2.ogg" #dun morogh idk
    show h happy at left
    s "Damn, vanilla WoW sure is nicer looking than I remember."
    h "yeah, isn't it cool?"
    s "Yeah, sure Hatkii..."
    h "try and tell me you're not having fun"
    s "I'm not having fun, Hatkii."
    h normal "..."
    "Maavrik joins the chat."
    show m kukuru at right
    m "jesus christ why are you playing this game"
    s "Because it's fun Maav, you should come join us!"
    m "no :^)"
    show black with fade
    "The minutes become hours, and the hours become days... Stovoy streams WoW for what seems like an eternity."
    "Maavrik wakes up the next day, and rejoins the chat..."
    hide black with fade
    m "how the fuck are you still streaming, it's been 9 hours bro"
    s "No way, it hasn't been 9 hours, you're bluffing."
    m "look at the chat, Stay_Hydrated_Bot has been talking to itself for the past 3 hours since your chat is dead as hell"
    Character("Stay_Hydrated_Bot", color="#1F87EC") "@stovoy You've been live for just over 24 hours. By this point in your broadcast you should have consumed at least 192oz (5.76L) of water to maintain optimum hydration."
    s "shit{nw}"
    s "Well, at least I'm having fun!"
    s "Hey Hatkii, Bunny, get over here.  {w}We're going to take a screenshot together, just like the old days!"
    h sad "i never had any friends so taking a screenshot isn't nostalgic for me"
    s "shut up hatkii"
    scene black with fade
    "The trio get into position..."
    scene bg wow ss
    s "Okay Bunny, get into position, and face the same direction as us."
    "{i}Bunny moves around indiscriminately.{/i}"
    s "Why isn't Bunny talking?"
    h "bunny doesn't have a mic"
    s "Right...  Well, we'll just wait for Bunny to get into position."
    "{i}Bunny fidgets around for about 30 seconds, rather than getting into position.{/i}"
    s "Okay, fuck it, I'm just going to take the screenshot."
    show bg wow ss
    #play sound screenshot.ogg
    with Fade(0.1, 0.0, 0.5, color="#fff")
    "{i}The screenshot is taken by Stovoy.{/i}"
    s "There we go, a perfect memory of our time in WoW!"
    show h normal at left
    h "this feels stupid to do"
    show p bripo at truecenter
    p "shut up hatkii"
    s "Whoa Polsaar, when did you get here?"
    p "i asked maavrik to be in the vn"
    show m kukuru at right
    m "yeah he did lol"
    s "Whatever...  Now, to begin our speedrun to Lv60."
    h "that's going to take months!"
    s "Guess it'll be a segmented speedrun. Lol."
    h "this is a stupid idea"
    m "shut up hatkii"

label wowending:
    scene black with fade
    "{b}Congratulations, you got the Addicted to WoW ending!{/b}"
    h "NOT SO FUCKING FAST{nw}"
    play sound "thunder.ogg"
    play music "rainloop.mp3" fadein 8
    "Hatkii yelled from behind the VN's end screen.{p}He was finally fed up. He has had enough."
    show h happy at truecenter
    h "Hello player.  Or should I say Stovoy, since who the fuck else is going to play this."
    h "I think it's about time I do what I said I would do in your chat."
    h "Take over this VN, and kill everyone."
    h evil "Soon, {b}nobody will tell me to shut up.{/b}"
    scene black
    play music "thunder.ogg"
    with None
    scene black with Fade(0, 0, 7)

    scene bg wow
    play music "wowmusic2.ogg"
    show h evil at left
    show p bripo at truecenter
    show m kukuru at right
    with fade
    s "Whoa Hatkii, what happened to you?"
    p "what the heckers, where did he get that BALLIN gold chain necklack from?"
    m "the fuck is this shit"
    h "You fools, you have tipped me over the edge.{p}Now, it is time for me to kill Stovoy."
    scene bg wow ded with dissolve
    s "What the shit Hatkii?"
    show h evil at left
    h "Bunny is next."
    s "fuck you hatkii."
    stop music
    h evilangry "WHAT DID YOU JUST SAY?"

    show m kukuru at truecenter
    m "lamo u got roasted br0"
    h "Maavrik.  I swear, I will kill you."
    m "lol try it kiddo you're like 6 years old"
    show p bripo at right
    p "lmao hatkii why are you such an angry little bitch lol"
    h "Polsaar.  You'll be next."
    p "what are you talking about you'll have to kill maav first lol {p}maav is invincible :3"
    m "what the fuck don't make claims i can't keep{p}i'm not invincible"
    p "you're telling me you shitpost radically and piss off countless people online, but you're not even invincible?{p}somebody could kill you at any time?"
    p "are you just waiting for death you hecc?"
    m "yes :({nw}"
    m "uhhhhh... {w}maybe lol"
    h evil "Shut up.  Both of you are dead anyways, and there's nothing you can do to stop me."
    h "But first, I'm going to take a swing at this player, since it's probably Stovoy."
    p "what are you even going to do, you don't even know how to fight"
    h "Rock Paper Scissors."
    m "what"
    p "are you fuckin dum"
    h evilangry "I'm going to obliterate you, player.  {w}The rules of this encounter are simple."
    scene black with fade
    show h evilangry at truecenter
    play music "<to 150.033>rps.ogg"
    h "First to 3 wins, wins the set.{p}Whoever loses this set, is vanquished forever.\n{size=10}you might want to save now{/size}"
    jump rps_select #scriptrps.rpy

label rps_winner:
    hide screen stats
    stop music fadeout 5
    show h at shake
    "You deliver a swift roundhouse kick to Hatkii.  He begins to die."
    play music "sadviolin.mp3"
    h evilsad "What the actual fuck?  All it takes is a simple roundhouse kick to do me in?"
    show s normal at left
    s "Maybe if you weren't such a weakling, this wouldn't have been the case."
    h "urgh... why did maav kill me in this vn..."
    show m kukuru at right
    m "because you wouldn't stop talking about it lol"
    m "eventually when something is said enough, you start to believe it yourself"
    h "why would you let yourself be influenced by ME?"
    m "mostly by choice lol"
    h "fuck..."
    hide h with dissolve
    stop music fadeout 3
    "Hatkii dies.  {w}Peace is restored."

    scene bg room with fade
    show s normal at left
    show p bripo at truecenter
    show m kukuru at right
    s "Wow, what a crazy ride with Hatkii!  Sure is a shame he had to die though."
    p "nah fuck that kid he was dum lol"
    m "for once i agree with pols"
    s "Well, shit...  Guess I better get back to streaming..."
    p "i don't even watch your stream, all i do is stop by and say i hate maavrik"
    m "he does :("
    s "Well, fuck you Polsaar, I'm going to go stream now."
    scene black with fade
    "{b}Congratulations, you got the \"Hatkiiller\" ending!{/b}"
    call credits from _call_credits
    return

label rps_gameover:
    hide screen stats
    stop music fadeout 5
    "Hatkii takes off his beautiful gold chain necklace, and garrotes you with it.{p}You feel your vision going black..."
    scene black with fade
    h "Don't worry about the semantics on how I am strangling a human through a VN.  You are dead now."
    "{b}You died to Hatkii's hand in [currentyear].  You got the Worst Ending.\n{/b}{size=10}you should reload that save you took before this{/size}"
    call credits from _call_credits_1
    return


transform shake:
        ease .06 yoffset 24
        ease .06 yoffset -24
        ease .05 yoffset 20
        ease .05 yoffset -20
        ease .04 yoffset 16
        ease .04 yoffset -16
        ease .03 yoffset 12
        ease .03 yoffset -12
        ease .02 yoffset 8
        ease .02 yoffset -8
        ease .01 yoffset 4
        ease .01 yoffset -4
        ease .01 yoffset 0
